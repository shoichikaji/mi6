#!/usr/bin/env raku
use App::Mi6;

my %*SUB-MAIN-OPTS = :named-anywhere;
my $app = App::Mi6.new;

multi MAIN("version") {
    say $app.dist.meta<version>;
}
multi MAIN("new", $module, :fez(:$zef), :$cpan, :$scaffold ) {
    $app.cmd("new", $module, :$cpan, :$scaffold );
}
multi MAIN("build") {
    $app.cmd("build");
}
multi MAIN("test", *@file, Bool :v(:$verbose), Int :j(:$jobs)) {
    $app.cmd("test", @file, :$verbose, :$jobs);
}
multi MAIN("release", Bool :k(:$keep), Str :$next-version, Bool :$yes) {
    $app.cmd("release", :$keep, :$next-version, :$yes);
}
multi MAIN("dist", Bool :$no-top-directory) {
    $app.cmd("dist");
}
multi MAIN("upload") {
    die "`mi6 upload` is removed, please execute `mi6 release` instead.\n";
}
multi MAIN("help") {
    USAGE();
}
multi MAIN(:h(:$help)!) {
    USAGE();
}
sub USAGE {
    require Pod::To::Text;
    ::("Pod::To::Text").render($=pod).say;
    exit 1;
}

=begin pod

=head1 USAGE

  $ mi6 new Foo::Bar # create Foo-Bar distribution
  $ mi6 build        # build the distribution, and re-generate README.md/META6.json
  $ mi6 test         # run tests
  $ mi6 release      # release your distribution tarball to Zef/CPAN ecosystem
  $ mi6 version      # show mi6 version
  $ mi6 help         # show this help

  Details:

  $ mi6 new Foo::Bar
    Create Foo-Bar distribution

    Options:
      --cpan      create distribution for CPAN ecosystem, instead of Zef ecosystem

      --scaffold  Use specified scaffolding to create new project.

                  If you specify a directory, the following optional files
                  will be used to scaffold the varous parts of your new project:
                    Changes dist module test license gitignore workflow

                  You can alternately specify one of the following flavours:
                    MIT - use the MIT license

  $ mi6 build
    Build the distribution, and re-generate README.md/META6.json.
    Here 'build' means if Build.pm is present, then execute it, otherwise nothing happens.

    Options:
     (none)

  $ mi6 test [test files/directory]
    Run tests

    Options:
     -v, --verbose   print all test lines
     -j, --jobs=N    run N test jobs in parallel

  $ mi6 release
    Create tarball for your distribution, and release it to Zef/CPAN ecosystem.

    Options:
     -k, --keep          do not delete local tarball after releasing it
         --next-version  set next release version manually
         --yes           do not raise prompt; accept the default behavior

  $ mi6 version
    Show mi6 version

    Options:
     (none)

  $ mi6 help, mi6 --help
    Show this help

    Options:
     -h, --help   show this help

=end pod
